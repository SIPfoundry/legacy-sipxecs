# Copyright (c) 2012 eZuce, Inc. All rights reserved.
# Contributed to SIPfoundry under a Contributor Agreement

# This software is free software; you can redistribute it and/or modify it under
# the terms of the Affero General Public License (AGPL) as published by the
# Free Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This software is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
# details.

#
# sipXrelay configuration and process management
#
bundle agent sipxrelay {
  methods:
    sipxrelay::
      "any" usebundle => "sipxrelay_config";
    any::
      "any" usebundle => "sipxrelay_setup";
      "any" usebundle => "sipxrelay_running";
}

bundle agent sipxrelay_config {
  files:
    any::
      "$(sipx.SIPX_CONFDIR)/nattraversalrules.xml"
        comment => "install NAT traversal rules $(this.promiser)",
        create => "true",
        perms => m("644"),
        copy_from => copy_from_cfdata("$(sipx.location_id)/nattraversalrules.xml"),
        classes => if_repaired("restart_sipxrelay");
}

bundle agent sipxrelay_setup {
  methods:
    !src.sipxrelay::
      "any" usebundle => rh_chkconfig_status("sipxrelay on");
    !src.!sipxrelay::
      "any" usebundle => rh_chkconfig_status("sipxrelay off");
}

bundle agent sipxrelay_running {
  vars:
    any::
      "service_command" string => ".*\s-Dprocname=sipxrelay\s.*";

  methods:
    any::
      "any" usebundle => find_sipxservice_by_command_regex("$(service_command)","sipxrelay_running");

  commands:
    sipxrelay.!sipxrelay_running.!stop_sipxecs::
      "$(sipx.SIPX_SERVICEDIR)/sipxrelay"
        comment => "start sipxrelay",
        args => "start";

    sipxrelay.sipxrelay_running.(restart_sipxrelay|restart_sipxecs)::
      "$(sipx.SIPX_SERVICEDIR)/sipxrelay"
        comment => "restart sipxrelay",
        args => "restart";

    (!sipxrelay|stop_sipxecs).sipxrelay_running::
      "$(sipx.SIPX_SERVICEDIR)/sipxrelay"
        comment => "stop sipxrelay",
        args => "stop";
}
