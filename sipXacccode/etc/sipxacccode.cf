# Copyright (c) 2012 eZuce, Inc. All rights reserved.
# Contributed to SIPfoundry under a Contributor Agreement

# This software is free software; you can redistribute it and/or modify it under
# the terms of the Affero General Public License (AGPL) as published by the
# Free Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This software is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
# details.

#
# sipxacccode configuration and process management
#
bundle agent sipxacccode {
  methods:
    sipxacccode::
      "any" usebundle => "sipxacccode_config";
    any::
      "any" usebundle => "sipxacccode_setup";
      "any" usebundle => "sipxacccode_running";
}

bundle agent sipxacccode_config {

  files:
    any::
      "$(sipx.SIPX_CONFDIR)/sipxacccode.properties"
        comment => "install auth codes config $(this.promiser)",
        create => "true",
        perms => m("644"),
        edit_line => sipxacccode_properties_contents,
        classes => if_repaired("restart_sipxacccode");

}

bundle edit_line sipxacccode_properties_contents {
  insert_lines:
    any::
      "$(sipx.SIPX_CFDATA)/$(sipx.location_id)/sipxacccode.properties.part"
        insert_type => "file";
      "acccode.sipxchangeDomainName=$(sipx.domain)";
      "acccode.realm=$(sipx.realm)";
      "log.file=$(sipx.SIPX_LOGDIR)/sipxacccode.log";
      "acccode.promptsDirectory=$(sipx.SIPX_IVRDATADIR)/prompts";
      "acccode.docDirectory=$(sipx.wwwdir)/doc";
  delete_lines:
    any::
      ".*";
}


bundle agent sipxacccode_setup {
  methods:
    !src.sipxacccode::
      "any" usebundle => rh_chkconfig_status("sipxacccode on");
    !src.!sipxacccode::
      "any" usebundle => rh_chkconfig_status("sipxacccode off");
}

bundle agent sipxacccode_running {
  vars:
    any::
      "service_command" string => ".*\s-Dprocname=sipxacccode\s.*";

  methods:
    any::
      "any" usebundle => find_sipxservice_by_command_regex("$(service_command)","sipxacccode_running");

  commands:
    (!sipxacccode|stop_sipxecs).sipxacccode_running::
      "$(sipx.SIPX_SERVICEDIR)/sipxacccode"
        comment => "stop sipxacccode",
        args => "stop";

    sipxacccode.!sipxacccode_running.!stop_sipxecs::
      "$(sipx.SIPX_SERVICEDIR)/sipxacccode"
        comment => "start sipxacccode",
        args => "start";

    sipxacccode.sipxacccode_running.(restart_sipxecs|restart_sipxacccode)::
      "$(sipx.SIPX_SERVICEDIR)/sipxacccode"
        comment => "restart sipxacccode",
        args => "restart";
}



