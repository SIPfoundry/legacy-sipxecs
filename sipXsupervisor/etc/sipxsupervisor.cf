# Copyright (c) 2012 eZuce, Inc. All rights reserved.
# Contributed to SIPfoundry under a Contributor Agreement

# This software is free software; you can redistribute it and/or modify it under
# the terms of the Affero General Public License (AGPL) as published by the
# Free Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This software is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
# details.

#
# This is sipxsupervisor the configuration and process management. Not to be 
# confused with sipxsupervisord.cf which is the rules for the sipxsupervisor
# running instance
#
bundle agent sipxsupervisor {
  # cannot think of a situtation were sipxsupervisor would ever be off 
  # (i.e. !sipxsupervisor) but none the less, let's support it here as it 
  # makes little difference to code.
  methods:
    sipxsupervisor::
      "any" usebundle => "sipxsupervisor_config";
      "any" usebundle => "sipxsupervisor_setup";
    any::
      "any" usebundle => "sipxsupervisor_running";
}

bundle agent sipxsupervisor_config {
  files:
    any::
      "$(sipx.SIPX_CONFDIR)/sipxsupervisor-allowed-addrs.ini"
        comment => "add allowed addresses to sipxsupervisor",
        create => "true",
        perms => m("644"),
	edit_defaults => empty,
        edit_line => sipxsupervisor_allowed_addrs,
        classes => if_repaired("restart_sipxsupervisor");

      "$(sipx.SIPX_CONFDIR)/sipxsupervisor-allowed-users.ini"
        comment => "add allowed users to sipxsupervisor",
        create => "true",
        perms => m("644"),
	edit_defaults => empty,
        edit_line => sipxsupervisor_allowed_users,
        classes => if_repaired("restart_sipxsupervisor");
}

bundle edit_line sipxsupervisor_allowed_addrs {
  vars:
    "part_file" string => "$(sipx.SIPX_CFDATA)/sipxsupervisor-allowed-addrs.part";

  classes:
    "part_exists" expression => fileexists("$(part_file)");

  insert_lines:
    part_exists.primary::
      "$(part_file)"
        insert_type => "file";

    !primary::  
      "$(sipx.master_address)";

    any::  
      "127.0.0.1";
      "::1";
}

bundle edit_line sipxsupervisor_allowed_users {
  vars:
    "part_file" string => "$(sipx.SIPX_CFDATA)/sipxsupervisor-allowed-users.part";

  classes:
    "part_exists" expression => fileexists("$(part_file)");

  insert_lines:
    part_exists::
      "$(part_file)"
        insert_type => "file";

    any::  
      "root";
      "$(sipx.SIPXPBXUSER)";
}

bundle agent sipxsupervisor_setup {
  methods:
    !src.sipxsupervisor::
      "any" usebundle => rh_chkconfig_status("sipxsupervisor on");
    !src.!sipxsupervisor::
      "any" usebundle => rh_chkconfig_status("sipxsupervisor off");
}

bundle agent sipxsupervisor_running {
  vars:
    any::
      "service_command" string => "/usr/sbin/cf-serverd";

  methods:
    any::
      "any" usebundle => find_service_by_command("$(service_command)","sipxsupervisor_running","root");

  commands:
    !sipxsupervisor.sipxsupervisor_running::
      "$(sipx.SIPX_SERVICEDIR)/sipxsupervisor"
        comment => "stop sipxsupervisor",
        args => "stop";

    sipxsupervisor.!sipxsupervisor_running.!stop_sipxecs::
      "$(sipx.SIPX_SERVICEDIR)/sipxsupervisor"
        comment => "start sipxsupervisor",
        args => "start";

    # NOTE: Restart in background because often supervisor is coordinating the restart command.
    # Delay 60 seconds but not tested as being optimal value. -- Douglas
    sipxsupervisor.sipxsupervisor_running.(restart_sipxecs|restart_sipxsupervisor)::
      "$(sipx.SIPX_CFMODULES)/run-later"
        comment => "restart sipxsupervisor in background",
        args => "60 $(sipx.SIPX_SERVICEDIR)/sipxsupervisor restart";
}

