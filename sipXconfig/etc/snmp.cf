# Copyright (c) 2012 eZuce, Inc. All rights reserved.
# Contributed to SIPfoundry under a Contributor Agreement

# This software is free software; you can redistribute it and/or modify it under
# the terms of the Affero General Public License (AGPL) as published by the
# Free Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This software is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
# details.

#
# Postgres configuration for working with sipxecs
#

bundle agent snmp {
  methods:
    snmp::
      "any" usebundle => "snmp_config";
    fedora16_or_greater::
      "any" usebundle => "snmp_systemctl_running";
    !fedora16_or_greater::
      "any" usebundle => "snmp_sysv_running";
}

bundle agent snmp_config {
  vars:
    any::
      "cron" string => "* * * * * $(sipx.SIPX_LIBEXECDIR)/snmp-fix-process-cron &>> $(sipx.SIPX_LOGDIR)/snmp-fix-process-cron.log";
      "cron_id" string => ".*/snmp-fix-process-cron .*";  #regex

  methods:
    snmp::
        "any" usebundle => add_crontab("$(cron_id)", "root", "$(cron)");
    !snmp::
        "any" usebundle => remove_crontab("$(cron_id)", "root");

  files:
    snmp::
      "/etc/snmp/snmpd.conf"
        comment => "SNMP service settings, $(this.promiser)",
        create => "true",
        perms => m("644"),
        edit_defaults => empty,
        edit_line => snmpd_conf,
        classes => if_repaired("reload_snmpd");

      "/etc/sysconfig/snmpd"
        comment => "SNMP service settings, $(this.promiser)",
        perms => m("644"),
        edit_defaults => empty,
        edit_line => insert_lines("OPTIONS=\"-A -Lf $(sipx.SIPX_LOGDIR)/snmpd.log -p /var/run/snmpd.pid\""),
        classes => if_repaired("reload_snmpd");
}

bundle edit_line snmpd_conf {
  vars:
    "part" string => "$(sipx.SIPX_CFDATA)/$(sipx.location_id)/snmpd.conf.part";

  #
  # Notes:
  #  -S on monitor to avoid race condition on snmp starting processes before cfengine
  #     has had a chance to start them.
  #
  #  informsink local send any traps created in snmpd to local snmptrapd server.
  #
  #  open permissions relying on firewall for restricting access.
  #
  insert_lines:
    any::
"com2sec local  localhost  public
com2sec local  $(sipx.master_address) public
com2sec local  0.0.0.0/0 public
group   rwgroup v1           local
group   rwgroup v2c           local
view    all    included   .1  80
access  rwgroup \"\"      any       noauth    exact  all none none
syslocation Unknown
syscontact Root <root@localhost>
informsink localhost
dontLogTCPWrappersConnects yes
createUser _internal
rwuser _internal
iquerySecName _internal
setEvent prFixIt prErrFix = 1
monitor -S -r 10 -e prFixIt \"procTable\" prErrorFlag != 0
";

    any::
     "$(part)"
       insert_type => "file",
       expand_scalars => "true";
}


bundle agent snmp_sysv_running {
  vars:
    any::
      "service" string => "/etc/init.d/snmpd";
      "service_command" string => "/usr/sbin/snmpd";

  methods:
    any::
      "any" usebundle => find_service_by_command("$(service_command)", "snmpd_running","root");
    !snmp::
      "any" usebundle => rh_chkconfig_status("snmpd off");
    snmp::
      "any" usebundle => rh_chkconfig_status("snmpd on");

  commands:
    (!snmp|stop_sipxecs).snmpd_running::
      "$(service)"
        comment => "stop snmp",
        args => "stop";

    snmp.!snmpd_running::
      "$(service)"
        comment => "start snmp",
        args => "start";

    # reload (-HUP) seems to kill snmpd process and not
    # start it back up again. 
    snmp.snmpd_running.reload_snmpd::
      "$(service)"
        comment => "restart snmpd",
        args => "restart";
}

bundle agent snmp_systemctl_running {
  vars:
    any::
      "service" string => "snmpd.service";

  classes:
    fedora16_or_greater::
      "snmpd_running" expression => returnszero("$(sipx.systemctl) is-active $(service)","noshell");

  commands:
    (!snmp|stop_sipxecs).snmpd_running::
      "$(sipx.systemctl)"
        comment => "stop snmpd",
        args => "stop $(service)";

      "$(sipx.systemctl)"
        comment => "disable snmpd",
        args => "disable $(service)";

    snmp.!snmpd_running::
      "$(sipx.systemctl)"
        comment => "start snmpd",
        args => "start $(service)";

      "$(sipx.systemctl)"
        comment => "enable snmpd",
        args => "enable $(service)";

    # reload (-HUP) seems to kill snmpd process and not
    # start it back up again. 
    snmp.snmpd_running.(reload_snmpd|restart_snmpd)::
      "$(sipx.systemctl)"
        comment => "restart snmpd",
        args => "restart $(service)";
}

