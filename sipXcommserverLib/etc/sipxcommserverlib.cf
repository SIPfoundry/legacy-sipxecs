# Copyright (C) 2012 eZuce Inc., certain elements licensed under a Contributor Agreement.
# Contributors retain copyright to elements licensed under a Contributor Agreement.
# Licensed to the User under the AGPL license.

#
# Common configure setup for sipXecs components
#

bundle agent sipxcommserverlib {
  methods:
    any::
      "any" usebundle => "sipxcommserverlib_config";
}

bundle agent sipxcommserverlib_config {
  classes:
    primary::
      "generate_certs" not => fileexists("$(sipx.SIPX_CONFDIR)/ssl/ssl.crt");

  files:
    primary::
      "$(sipx.SIPX_CONFDIR)/SSL_DEFAULTS"
        comment => "install SSL info $(this.promiser)",
        create => "true",
        perms => m("644"),
        edit_line => SSL_DEFAULTS,
        classes => if_repaired("generate_certs");

    any::
      "$(sipx.SIPX_CONFDIR)/domain-config"
        comment => "install domain info $(this.promiser)",
        create => "true",
        perms => m("644"),
        edit_line => domain_config,
        classes => if_repaired("restart_sipxproxy");

        # do not call this, it restarts too much,
        # need to get DOMAIN_ALIASES out of the config file
        # classes => if_repaired("restart_sipxecs");

  methods:
    generate_certs::
      "any" usebundle => "generate_certs";
}

bundle agent generate_certs {
  vars:
    any::
      "tempssl" string => "$(sipx.SIPX_VARDIR)/temp/cert-temp";
       
  files:
    any::
      "$(tempssl)/."
         create => "true";

  commands:
    any::
      "$(sipx.SIPX_BINDIR)/ssl-cert/gen-ssl-keys.sh"
        comment => "generate ssl keys",
        args => "-d -p $(sipx.SIPX_CONFDIR)/SSL_DEFAULTS",
        contain => sipxcommserverlib_cert_cmd("$(tempssl)");

      "$(sipx.SIPX_BINDIR)/ssl-cert/install-cert.sh",
        comment => "install ssl keys",
        contain => sipxcommserverlib_cert_cmd("$(tempssl)");
}

body contain sipxcommserverlib_cert_cmd(d) {
  chdir => "$(d)";
}

bundle edit_line SSL_DEFAULTS {
  insert_lines:
    "countryName=\"US\"";
    "stateOrProvinceName=\"AnyState\"";
    "localityName=\"AnyTown\"";
    "organizationName=\"$(sipx.domain)\"";
    "organizationalUnitName=\"IT\"";
    "caName=\"ca.$(sipx.domain)\"";
    "caEmail=\"root@$(sipx.domain)\"";
    "sipDomainName=\"$(sipx.domain)\"";
    "server=\"$(sipx.host).$(sipx.net_domain)\"";
    "serverEmail=\"root@$(sipx.host).$(sipx.net_domain)\"";
    
  delete_lines:
    ".*";
}

bundle edit_line domain_config {
  classes:
    "part_exists" expression => fileexists("$(sipx.SIPX_CFDATA)/domain-config.part");

  insert_lines:
    any::
      "SIP_DOMAIN_NAME : $(sipx.domain)";
      "SIP_REALM : $(sipx.realm)";
      "SHARED_SECRET : $(sipx.secret)";
      "DEFAULT_LANGUAGE : $(sipx.lang)";
      "SUPERVISOR_PORT : 8092";
    part_exists::
      "$(sipx.SIPX_CFDATA)/domain-config.part"
        insert_type => "file";
    !part_exists::
       "SIP_DOMAIN_ALIASES : $(sys.ipv4) $(sipx.host).$(sipx.net_domain)";
       "CONFIG_HOSTS : $(sipx.host).$(sipx.net_domain)";

  delete_lines:
    any::
      ".*";
}
