# Copyright (C) 2012 eZuce Inc., certain elements licensed under a Contributor Agreement.
# Contributors retain copyright to elements licensed under a Contributor Agreement.
# Licensed to the User under the AGPL license.

#
# Common configure setup for sipXecs components
#

bundle agent sipxcommserverlib {
  methods:
    any::
      "any" usebundle => "sipxcommserverlib_config";
}

bundle agent sipxcommserverlib_config {
  classes:
    "generate_certs" not => fileexists("$(sipx.SIPX_CONFDIR)/ssl/ssl.crt");

  files:
    primary::
      "$(sipx.SIPX_CONFDIR)/SSL_DEFAULTS"
        create => "true",
        perms => m("644"),
        edit_line => SSL_DEFAULTS,
        classes => if_repaired("generate_certs"),
	action => track("$(this.promiser)");      

      "$(sipx.SIPX_CONFDIR)/domain-config"
        create => "true",
        perms => m("644"),
        edit_line => domain_config,
        classes => if_repaired("restart_sipxecs"),
	action => track("$(this.promiser)");

  methods:
    generate_certs::
      "any" usebundle => "generate_certs";
}

bundle agent generate_certs {
  files:
    any::
      "$(sipx.SIPX_VARDIR)/temp/cert-temp/."
         create => "true";

  commands:
    any::
      "$(sipx.SIPX_BINDIR)/ssl-cert/gen-ssl-keys.sh"
        args => "-d -p $(sipx.SIPX_CONFDIR)/SSL_DEFAULTS",
	contain => sipxcommserverlib_cert_cmd,
	action => track("gen-ssl-keys");

      "$(sipx.SIPX_BINDIR)/ssl-cert/install-cert.sh",
	contain => sipxcommserverlib_cert_cmd,
	action => track("install-cert");
}

body contain sipxcommserverlib_cert_cmd {
  chdir => "$(sipx.SIPX_VARDIR)/temp/cert-temp";
}

bundle edit_line SSL_DEFAULTS {
  insert_lines:
    "countryName=\"US\"";
    "stateOrProvinceName=\"AnyState\"";
    "localityName=\"AnyTown\"";
    "organizationName=\"$(sipx.domain)\"";
    "organizationalUnitName=\"IT\"";
    "caName=\"ca.$(sipx.domain)\"";
    "caEmail=\"root@$(sipx.domain)\"";
    "sipDomainName=\"$(sipx.domain)\"";
    "server=\"$(sys.fqhost)\"";
    "serverEmail=\"root@$(sys.fqhost)\"";
    
  delete_lines:
    ".*";
}

bundle edit_line domain_config {
  classes:
    "part_exists" expression => fileexists("$(sipx.SIPX_CFDATA)/domain-config.part");

  insert_lines:
    any::
      "SIP_DOMAIN_NAME : $(sipx.domain)";
      "SIP_REALM : $(sipx.realm)";
      "SHARED_SECRET : $(sipx.secret)";
      "DEFAULT_LANGUAGE : $(sipx.lang)";
      "SUPERVISOR_PORT : 8092";
    part_exists::
      "$(sipx.SIPX_CFDATA)/domain-config.part"
        insert_type => "file";
    !part_exists::
       "SIP_DOMAIN_ALIASES : $(sys.ipv4) $(sipx.domain)";
       "CONFIG_HOSTS : $(sys.fqhost)";

  delete_lines:
    any::
      ".*";
}
